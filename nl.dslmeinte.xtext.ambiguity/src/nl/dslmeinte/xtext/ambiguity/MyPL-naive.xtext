grammar nl.dslmeinte.xtext.ambiguity.MyPL with org.eclipse.xtext.common.Terminals

generate myPL "http://www.dslmeinte.nl/xtext/ambiguity/MyPL"


Program: elements += (Class | Statement)*;


Class: 'class' name=ID '{' features+=Feature* '}';
Feature:	Class | Attribute;
Attribute:	name=ID ':' type=ID;		// (a DataType enum is nicer but unnecessary for the example)


Expression: IntegerLiteral;
IntegerLiteral: int=INT;


// our naive grammar:

FeatureRefTail:
    '.' feature=[Feature] tail=FeatureRefTail?;


Statement:
    VariableDeclaration | Assignment;


VariableDeclaration:
    typeRef=ClassRef variable=Variable (':=' value=Expression)?;

ClassRef:
    type=[Class] tail=FeatureRefTail?;

Variable:
	name=ID;


Assignment:
    lhs=AssignableSite ':=' value=Expression;

AssignableSite:
    var=[VariableDeclaration] tail=FeatureRefTail?;

