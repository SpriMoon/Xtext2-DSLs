grammar nl.dslmeinte.xtext.express.Express with nl.dslmeinte.xtext.express.expression.Expression

generate express "http://www.dslmeinte.nl/xtext/express/Express"


// §9.3  Schema

Schema:
	'schema' name=ID ';' constants+=ConstantsDeclaration? declarations+=Declaration* 'end_schema' ';'
	;


/*
 * +----+
 * | §8 |
 * +----+
 */

DataType:
	SimpleDataType | AggregationDataType | NamedDataType | ConstructedDataType // | GeneralizedDataType
	;

// §8.1

SimpleDataType:
	NumberDataType | RealDataType | IntegerDataType | LogicalDataType | BooleanDataType | StringDataType | BinaryDataType
	;

// (the unparametrized simple data types:)
NumberDataType:  {NumberDataType}  'number' ;
IntegerDataType: {IntegerDataType} 'integer';
LogicalDataType: {LogicalDataType} 'logical';
BooleanDataType: {BooleanDataType} 'boolean';

RealDataType:
	{RealDataType} 'real' ( '(' precision=INT ')' )?
	;
	// TODO  precision=numeric_expression

StringDataType:
	{StringDataType} 'string' ( '(' width=INT ')' fixed?='fixed'? )?
	;
	// TODO  width=numeric_expression

BinaryDataType:
	{BinaryDataType} 'binary' ( '(' width=INT ')' fixed?='fixed'? )?
	;
	// TODO  width=numeric_expression

// §8.2

AggregationDataType:
	ArrayDataType | ListDataType | BagDataType | SetDataType
	;

ArrayDataType:
	'array' '[' from=INT ':' to=INT ']' 'of' optional?='optional'? unique?='unique'? type=BaseType
	;
	// TODO  from,to=numeric_expression

ListDataType:
	'list' ( '[' from=INT ':' to=INT ']' )? 'of' unique?='unique'? type=BaseType
	;

BagDataType:
	'bag' ( '[' from=INT ':' to=INT ']' )? 'of' type=BaseType
	;

SetDataType:
	'set' ( '[' from=INT ':' to=INT ']' )? 'of' type=BaseType
	;

NamedDataType:
	Entity // | DefinedDataType
	;

// §8.4

ConstructedDataType:
	Enumeration | SelectDataType
	;

Enumeration:	// not using the 'DataType' postfix because 'Enumeration' already exudes the data type semantics
	'enumeration' 'of' '(' literals+=ID ( ',' literals+=ID )* ')'
	;

SelectDataType:
	'select' '(' types+=[NamedDataType] ( ',' types+=[NamedDataType] )* ')'
	;

GeneralizedDataType:
	AggregationDataType // | GeneralAggregationDataType | GenericType
	;

// §8.6

BaseType:	// (§8.6.1)
	AggregationDataType | SimpleDataType | NamedDataType
	;

ParameterDataType:	// (§8.6.2)
	GeneralizedDataType | NamedDataType | SimpleDataType
	;

UnderlyingDataType:	// (§8.6.3)
	ConstructedDataType | AggregationDataType | SimpleDataType | TypeReference
	;

TypeReference:	// (§8.3.2)
	type=[NamedDataType]
	;


/*
 * +----+
 * | §9 |
 * +----+
 */

Declaration:
	TypeDecl | Entity
	;

// §9.1

TypeDecl:
	'type' name=ID '=' type=UnderlyingDataType ';' whereClause=WhereClause? 'end_type' ';'
	;

terminal WhereClause:
	"where" (. -> ';')
	;
	// FIXME  this is an island grammar-style solution to not explicitly parser where clauses (not it only "parses" one DomainRule)

// §9.2

Entity:		// not using the 'DataType' postfix because 'Entity' already exudes the data type semantics
	'entity' name=ID /* TODO  [ subsuper ] */';'
		explicitAttributesDecl+=ExplicitAttributeDecl
		// TODO  derive-clause?, inverse-clause?, unique-clause?
		whereClause=WhereClause?
	'end_entity' ';'
	;

Attribute:
	ExplicitAttributeDecl
	;

ExplicitAttributeDecl:
	decls+=AttributeDecl ( ',' decls+=AttributeDecl )* ':' optional?='optional'? type=BaseType ';'
	;

AttributeDecl:
	AttributeID | QualifiedAttributeID
	;

AttributeID: name=ID;

QualifiedAttributeID hidden():
	'self' '\\' entity=[Entity] '.' attribute=[Attribute]
	;


// §9.4  Constant

ConstantsDeclaration:
	'CONSTANT'
	declarations+=ConstantDeclaration+
	'END_CONSTANT' ';'
	;

ConstantDeclaration:
	name=ID ':' type=BaseType ':=' /* TODO  value=Expression */';'
	;


